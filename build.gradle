plugins {
	id 'java'
	id 'application'
	id "com.diffplug.spotless" version "6.14.0"
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
	mavenCentral()
}

dependencies {
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
	implementation 'org.mongodb:mongo-java-driver:3.12.11'
	implementation 'com.sparkjava:spark-core:2.9.4'
	implementation 'org.slf4j:slf4j-simple:2.0.5'
}

test {
	useJUnitPlatform()
}

jar {
	manifest {
		attributes "Main-Class": "com.gradle.tutorial.FizzBuzzProcessor"
	}

	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
}

application {
	getMainClass().set('com.gradle.tutorial.FizzBuzzProcessor')
}

spotless {
	// optional: limit format enforcement to just the files changed by this feature branch
	ratchetFrom 'origin/main'

	format 'misc', {
		// define the files to apply `misc` to
		target '*.gradle', '*.md', '.gitignore'

		// define the steps to apply to those files
		trimTrailingWhitespace()
		indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
		endWithNewline()
	}
	java {
		// don't need to set target, it is inferred from java

		// use the default importOrder configuration
		importOrder()

		// apply a specific flavor of google-java-format
		googleJavaFormat('1.8').aosp().reflowLongStrings()
		// fix formatting of type annotations
		formatAnnotations()
		// make sure every file has the following copyright header.
		// optionally, Spotless can set copyright years by digging
		// through git history (see "license" section below)
		licenseHeader '/* (C)$YEAR */'
	}
	javascript {
		target 'src/**/*.js' // you have to set the target manually

		prettier() // has its own section below
		// eslint() // has its own section below

		// licenseHeader '/* (C) $YEAR */', '' // or licenseHeaderFile
	}
}
